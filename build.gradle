plugins {
    id 'java-library'
    id 'application'
    id 'eclipse'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(18))
    }
}

group 'com.kousenit'
version '1.0'

application {
    mainClass.set('com.kousenit.fileio.ProcessDictionary')
    applicationDefaultJvmArgs = ['--enable-preview']
}

repositories {
    mavenCentral()
}

String assertJVersion = '3.22.0'
String gsonVersion = '2.9.0'
String hamcrestVersion = '2.2'
String jacksonVersion = '2.13.2'
String jqwikVersion = '1.6.5'
String junitVersion = '5.8.2'
String mockitoVersion = '4.3.1'
String moshiVersion = '1.13.0'
String wiremockversion = '2.32.0'

dependencies {
    // JSON parsers
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "net.jqwik:jqwik:$jqwikVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testImplementation "com.github.tomakehurst:wiremock-jre8:$wiremockversion"
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs.add('--enable-preview')
}

test {
    useJUnitPlatform()
    jvmArgs(['--enable-preview'])
}
