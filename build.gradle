plugins {
    id 'java-library'
    id 'application'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(16))
        // vendor = JvmVendorSpec.ADOPTOPENJDK
    }
}

group 'com.kousenit'
version '1.0'

application {
    mainClass.set('com.kousenit.fileio.ProcessDictionary')
    applicationDefaultJvmArgs = ['--enable-preview']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.squareup.moshi:moshi:1.12.0'
    implementation 'com.squareup.moshi:moshi-adapters:1.12.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "net.jqwik:jqwik:1.5.1"
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs.add('--enable-preview')
    // it.options.compilerArgs.addAll(['--release', '15'])
}

compileTestJava {
    options.compilerArgs += '-parameters'
}

test {
    useJUnitPlatform {
        includeEngines 'jqwik', 'junit-jupiter'
    }
    jvmArgs(['--enable-preview'])
}
